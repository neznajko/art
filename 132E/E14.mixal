TERM	EQU	19		Telewriter terminal
ZAP	EQU	41		,
	ORIG	1000
BUF	ORIG	*+14		Output buffer
* func spec
* > rI3 - Year (Y)
* .   N - Date of Easter
*   rI2 - MONTH index
EASTER	STJ	EXIT
* E1.[Golden number.] Set G <- (Y mod 19) + 1.
E1	ENTA	0,3	
	SRAX	5
	DIV	=19=		rX contains the remainder
	INCX	1
	STX	G
* E2.[Century.] Set C <- floor(Y/100) + 1.
E2	ENTA	0,3		
	SRAX	5
	DIV	=100=
	INCA	1
	STA	C
* E3.[Corrections.] Set X <- floor(3C/4) - 12,
*    Z <- floor((8C + 5)/25) - 5
E3	MUL	=3=		
	DIV	=4=
	SUB	=12=
	STA	X
	LDA	C
	MUL	=8=		rA:rX holds the result
	SLAX	5
	ADD	=5=
	SRAX	5
	DIV	=25=
	SUB	=5=
	STA	Z
* E4. [Find Sunday] D <- floor(5*Y/4) - X - 10
E4    	ENTA 	0,3
	MUL	=5=
	DIV	=4=
	SUB	X
	SUB	=10=
	STA	D	
* E5. [Epact.] Set E <- (11G + 20 + Z - X) mod 30. If E = 25 and
*     the golden number G is greater than 11, or if E = 24,
*     then increase E by 1.
E5    	LDA	G
	MUL	=11=
	SLAX	5
	ADD	=20=
	ADD	Z
	SUB	X
	SRAX	5
	DIV	=30=
	CMPX	=24=
	JE	1F
	CMPX	=25=
	JNE	2F
	LDA	G
	CMPA	=11=
	JLE	2F
1H	INCX	1
2H	STX	E
* E6. [Find full moon.] Set N <- 44 - E. If N < 21 then set N <- N + 30.
E6      ENTA	44
	SUB	E
	CMPA	=21=
	JGE	1F
	ADD	=30=
1H	STA	N
* E7. [Advance to Sunday.] Set N <- N + 7 - ((D + N) mod 7)
E7      ADD     D
	SRAX	5
	DIV	=7=
	ENNA	0
	SLAX	5
	ADD	=7=
	ADD	N
	STA	N
* E8. [Get month.] If N > 31, the date is (N - 31) APRIL; otherwise the
*     date is N MARCH.
E8     	CMPA  	=31=
	JG	1F
	ENT2	0
	JMP	2F
1H	ENT2	1
	SUB	=31=
2H	STA	N
EXIT	JMP	*
START	ENT3	1950
  	ENT1	BUF 
1H	JMP	EASTER
	LDA	N
	CHAR	0		rX holds the key
	SLAX	1		Add ' ' to the right of N
	LDA	MONTH,2		Load Month to rA
	SLC	2		rA:rX |R|I|L| | |:|0|9| |A|P|
	STX	0,1
	SLC	3		rA:rX | | | | | |:| | |R|I|L|
	ENTA	ZAP
	SLA	4
	SLC	2		rA:rX | | | | | |:|R|I|L|,| |
	STX	1,1
	ENTA	0,3
	CHAR	0
	STX	2,1
	OUT	0,1(TERM)
	INC3	1
	CMP3	=2000=
	JLE	1B
	HLT
G	CON	0		Golden number
C	CON	0		Century
X	CON	0		Correction
Z	CON	0		Correction
D	CON	0		Sunday
E	CON	0		Epact
N	CON	0		Day of Easter
MONTH	ALF	MARCH
	ALF	APRIL
	END	START
