TERM	EQU	19		TYPEWRITER TERMINAL
PPER	EQU	20		PAPER TAPE
B	EQU	14		BLOCK SIZE
NCHR	EQU	56		NUMBER OF CHARACTERS
	ORIG	0
FRQC	ORIG	*+NCHR		FREQUENCY COUNT
COD0	ORIG	*+B		INPUT BUFFERS
	CON	*+1		
COD1	ORIG	*+B
	CON	COD0		
ASTX	CON	46		ASTERIX
BUF	ORIG	*+B		OUTPUT BUFFER
* rI5 - COUNTING BUFFER
* rI6 - READING  BUFFER
START	ENT5	COD0
	IN	0,5(PPER)	READ FIRST LINE
	JBUS	*(PPER)
1H	LD6	B,5	
	IN	0,6(PPER)
	ENT3	B		LOOP COUNTER
2H	LD4	0,5		USE CHR CODE AZ A FRQC OFST
	J4Z	3F		SPACE
	CMP4	ASTX		ARE WE DONE?
	JE	SPIT
	LDA	FRQC,4
	INCA	1		
	STA	FRQC,4		
3H	DEC3	1
	INC5	1
	J3NZ	2B
	ENT5	0,6		SWITCH BUFFERS
	JMP	1B
SPIT	ENT3	-NCHR+1
1H	LDA	FRQC+NCHR,3
	JAZ	2F
	CHAR	0
	STA	BUF(4:5)
	ENTA	NCHR,3
	SLA	2
	STA	BUF(1:3)
	STX	BUF+1
	OUT	BUF(TERM)
2H	INC3	1
	J3N	1B
	HLT
	END	START
