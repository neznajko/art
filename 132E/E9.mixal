* INSTRUCTION FIELDS
ADR	EQU	0:2		ADDRESS
IDX	EQU	3:3		INDEX
FLD 	EQU   	4:4		FIELD
OPC	EQU	5:5		OPCODE
* TABLE FIELDS
M	EQU	4:4		MX L,R OR F
K	EQU	5:5		KEY
	ORIG	3000
* CHECK STJ
CKSTJ	ENT3	=2=
	JMP	1F
* CHECK 8*L + R
CKLR	ENT3	=5=
1H	LDA	INST(FLD)
	SRAX	5
	DIV	=8=
	CMPA	0,3
	JG	BAD
	CMPX	0,3
	JG	BAD
	JMP	CKIDX
* CHECK IF F IS LESS THAN MX
* ENTRY: rI1 TABLE INDEX
CKMX 	LDA  	TABLE,1(M)	MX
	CMPA	INST(FLD)
	JG	BAD	 
	JMP	CKIDX	
TABLE	CON	3		NOP
	CON	0		ADD
	CON	0		SUB
	CON	0		MUL
	CON	0		DIV
	CON	2(M),2(K)	NUM,CHAR,HLT
	CON	5(M),2(K)	SLA,SRA,SLAX,SRAX,SLC,SRC
	CON	4		MOVE
*
	ORIG	*+24            LDG, LDGN AND STG
*
	CON	1		STJ
	CON	0		STZ
	CON	2		JBUS
	CON	2		IOC
	CON	2		IN
	CON	2		OUT
	CON	2		JRED
	CON	9(M),2(K)	JMP,JSJ,...,JLE
*
	CON	5(M),2(K)	JA[+]
	CON	5(M),2(K)	J1[+]
	CON	5(M),2(K)	J2[+]		
	CON	5(M),2(K)	J3[+]	
	CON	5(M),2(K)	J4[+]	
	CON	5(M),2(K)	J5[+]	
	CON	5(M),2(K)	J6[+]	
	CON	5(M),2(K)	JX[+]	
*
	CON	3(M),2(K)	INCA,DECA,ENTA,ENNA
	CON	3(M),2(K)	INC1,DEC1,ENT1,ENN1
	CON	3(M),2(K)	INC2,DEC2,ENT2,ENN2
	CON	3(M),2(K)	INC3,DEC3,ENT3,ENN3
	CON	3(M),2(K)	INC4,DEC4,ENT4,ENN4
	CON	3(M),2(K)	INC5,DEC5,ENT5,ENN5
	CON	3(M),2(K)	INC6,DEC6,ENT6,ENN6
	CON	3(M),2(K)	INCX,DECX,ENTX,ENNX
*
	ORIG	*+8		CMPG
INST	CON	-50(ADR),3(IDX),4(FLD),6(OPC)
START	LD1	INST(OPC)
	LD2	TABLE,1(K)
	JMP	*+1,2		SWITCH
	JMP	CKLR
	JMP	CKSTJ
	JMP	CKMX
	JMP	GOOD
CKIDX	LDA	INST(IDX)
	CMPA	=6=
	JG	BAD
	JANZ	GOOD
CKADR	LDA	INST(ADR)
	JAN	BAD
	CMPA	=4000=
	JGE	BAD
GOOD	NOP
BAD	NOP	
	HLT
	END	START
